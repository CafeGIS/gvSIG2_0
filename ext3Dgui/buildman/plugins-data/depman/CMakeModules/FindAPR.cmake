# Locate Apache Portable Runtime including APR-util and APR-xml (aka expat)
# APR_LIBRARIES
# APR_FOUND
# APR_INCLUDE_DIRS

IF(UNIX AND NOT APPLE)

	# LINUX uses only pkg-config installed APR and APR-util
	FIND_PACKAGE(PkgConfig REQUIRED)
	FIND_PACKAGE(EXPAT REQUIRED)
	PKG_SEARCH_MODULE(APRCORE REQUIRED apr-1)
	PKG_SEARCH_MODULE(APRUTIL REQUIRED apr-util-1)

	# SET variables
	IF(EXPAT_FOUND AND APRCORE_FOUND AND APRUTIL_FOUND)
		SET(APR_FOUND 1)
		SET(APR_INCLUDE_DIRS ${APRCORE_INCLUDE_DIRS} ${APRUTIL_INCLUDE_DIRS} ${EXPAT_INCLUDE_DIRS})
		SET(APR_LIBRARIES ${APRCORE_LIBRARIES} ${APRUTIL_LIBRARIES} ${EXPAT_LIBRARIES})
	ENDIF(EXPAT_FOUND AND APRCORE_FOUND AND APRUTIL_FOUND)

ELSE(UNIX AND NOT APPLE)

	IF(APPLE)
		FIND_PACKAGE(EXPAT REQUIRED)
	ELSE(APPLE)
		FIND_PATH(EXPAT_INCLUDE_DIRS apr_xml.h ${DEPMAN_PATH}/include/apr-1)
		FIND_LIBRARY(EXPAT_LIBRARIES NAMES xml)
		MARK_AS_ADVANCED(EXPAT_INCLUDE_DIRS EXPAT_LIBRARIES)
		IF(EXPAT_INCLUDE_DIRS AND EXPAT_LIBRARIES)
			SET(EXPAT_FOUND 1)
		ENDIF(EXPAT_INCLUDE_DIRS AND EXPAT_LIBRARIES)
	ENDIF(APPLE)

	FIND_PATH(APR_INCLUDE_DIR apr.h ${DEPMAN_PATH}/include/apr-1)
	MARK_AS_ADVANCED(APR_INCLUDE_DIR)

	FIND_LIBRARY(APR_apr_LIBRARY NAMES apr apr-1)
	MARK_AS_ADVANCED(APR_apr_LIBRARY)

	FIND_LIBRARY(APR_aprutil_LIBRARY NAMES aprutil aprutil-1)
	MARK_AS_ADVANCED(APR_aprutil_LIBRARY)

	IF(APR_apr_LIBRARY AND APR_aprutil_LIBRARY AND EXPAT_LIBRARIES)
		SET(APR_LIBRARIES ${APR_aprutil_LIBRARY} ${APR_apr_LIBRARY} ${EXPAT_LIBRARIES})
	ENDIF(APR_apr_LIBRARY AND APR_aprutil_LIBRARY AND EXPAT_LIBRARIES)

	IF(APR_INCLUDE_DIR AND EXPAT_INCLUDE_DIRS)
		SET(APR_INCLUDE_DIRS ${APR_INCLUDE_DIR} ${EXPAT_INCLUDE_DIRS})
	ENDIF(APR_INCLUDE_DIR AND EXPAT_INCLUDE_DIRS)

	SET(APR_FOUND 0)
	IF(APR_INCLUDE_DIRS AND APR_LIBRARIES)
		SET(APR_FOUND 1)
		IF(WIN32)
			ADD_DEFINITIONS(-DAPR_DECLARE_STATIC -DAPU_DECLARE_STATIC)
			SET(APR_LIBRARIES ws2_32.lib mswsock.lib rpcrt4.lib ${APR_LIBRARIES})
		ENDIF(WIN32)
	ENDIF(APR_INCLUDE_DIRS AND APR_LIBRARIES)

ENDIF(UNIX AND NOT APPLE)

